buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

import groovy.json.*

configurations {
    compile
    deployJars
}

apply plugin: "forge"
apply plugin: "maven"

group = "net.doubledoordev.mtrm"
version = "1.0.0"

targetCompatibility = 1.7
sourceCompatibility = 1.7

archivesBaseName = 'MineTweakerRecipeMaker'
def githuborg = 'DoubleDoorDevelopment'
def description = 'MineTweaker Recipe Maker'

minecraft {
    version = "1.7.10-10.13.1.1224"
    runDir = "jars"
}

if (System.getenv().BUILD_NUMBER != null) version += "." + System.getenv().BUILD_NUMBER

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version, 'modid':project.archivesBaseName, 'githuborg':githuborg, 'description':description
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
    }
}

task sourcesJar(type: Jar) {
    from "LICENSE.txt"
    from sourceSets.main.allSource
    classifier = 'src'
    appendix = project.minecraft.version
}

task deobfJar(type: Jar) {
    from "LICENSE.txt"
    from sourceSets.main.output
    from (sourceSets.main.allSource.srcDirs) {
        include '**/*.java'
    }
	classifier = 'dev'
    appendix = project.minecraft.version
}

jar {
    from "LICENSE.txt"
    appendix = project.minecraft.version
}

artifacts {
	archives jar
    archives sourcesJar
    archives deobfJar
}